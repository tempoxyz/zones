// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {
    BlockTransition,
    Capability,
    Deposit,
    DepositPayload,
    DepositQueueTransition,
    DepositType,
    ENCRYPTION_KEY_GRACE_PERIOD,
    EncryptionKeyEntry,
    IVerifier,
    IZoneMessenger,
    IZonePortal,
    MAX_WITHDRAWAL_CALLBACK_GAS,
    QueuedDeposit,
    Role,
    TokenConfig,
    Withdrawal,
    WithdrawalBounceBackDeposit,
    ZONE_FACTORY_ADDRESS,
    ZONE_PORTAL_IMPL_ADDRESS
} from "../interfaces/IZone.sol";
import { getBlockHash } from "../libraries/BlockHashHistory.sol";
import { DepositQueueLib } from "../libraries/DepositQueueLib.sol";
import { ENCRYPTED_PAYLOAD_PLAINTEXT_SIZE } from "../libraries/EncryptedDeposit.sol";
import { Secp256k1Lib } from "../libraries/Secp256k1Lib.sol";
import { WithdrawalQueue, WithdrawalQueueLib } from "../libraries/WithdrawalQueueLib.sol";
import { StdPrecompiles } from "tempo-std/StdPrecompiles.sol";
import { ITIP20 } from "tempo-std/interfaces/ITIP20.sol";
import { ITIP20Factory } from "tempo-std/interfaces/ITIP20Factory.sol";
import { ITIP403Registry } from "tempo-std/interfaces/ITIP403Registry.sol";

/// @title ZonePortal
/// @notice Per-zone portal that escrows zone tokens on Tempo and manages deposits/withdrawals
contract ZonePortal is IZonePortal {

    using WithdrawalQueueLib for WithdrawalQueue;

    /*//////////////////////////////////////////////////////////////
                               CONSTANTS
    //////////////////////////////////////////////////////////////*/

    /// @notice TIP-403 registry for transfer policy authorization checks
    ITIP403Registry internal constant TIP403_REGISTRY =
        ITIP403Registry(StdPrecompiles.TIP403_REGISTRY_ADDRESS);

    /// @notice Fixed gas value for deposit fee calculation
    /// @dev Set to 100,000 gas. Deposit fee = FIXED_DEPOSIT_GAS * zoneGasRate.
    ///      This provides a stable pricing basis for deposits while allowing the admin
    ///      to adjust the zoneGasRate based on operational costs.
    uint64 public constant FIXED_DEPOSIT_GAS = 100_000;

    /// @notice Maximum deposits that may be appended to this portal in one Tempo block.
    /// @dev Under T9, processing 230 encrypted deposits rejected by the issuer's
    ///      TIP-403 transfer policy uses 193,044,874 gas, leaving 6,955,126 gas
    ///      below the buffered 200,000,000 gas ceiling.
    uint64 public constant MAX_DEPOSITS_PER_TEMPO_BLOCK = 230;

    /// @notice Maximum tokens that may be enabled for this portal in one Tempo block.
    /// @dev Under T9, processing 230 worst-case deposits plus 8 token enablements with maximum
    ///      metadata uses 214,832,282 gas, below the buffered 225,000,000 gas ceiling.
    uint64 public constant MAX_TOKENS_ENABLED_PER_TEMPO_BLOCK = 8;

    /// @notice Maximum byte length of each token metadata string copied into the zone.
    /// @dev Keeps name, symbol, and currency in Solidity's one-slot short-string representation.
    uint256 public constant MAX_TOKEN_METADATA_BYTES = 31;

    /// @dev Reserves enough capacity for one maximum-size sequencer withdrawal batch to bounce.
    ///      The 20M batch gas ceiling fits at most 19 simple withdrawals (plus one slot of margin).
    uint64 internal constant WITHDRAWAL_BOUNCEBACK_RESERVE = 20;

    /// @notice Scale factor from 18-decimal Tempo gas prices to 6-decimal TIP-20 units
    uint256 internal constant TEMPO_BASE_FEE_SCALE = 1e12;

    /// @notice Maximum gas a withdrawal callback may request
    /// @dev Over-cap legacy withdrawals are dequeued and bounced back in `processWithdrawals`.
    uint64 public constant MAX_WITHDRAWAL_GAS_LIMIT = MAX_WITHDRAWAL_CALLBACK_GAS;

    /// @notice Maximum allowed gas fee rate to prevent overflows
    uint128 public constant MAX_GAS_FEE_RATE = 1e18;

    /// @notice Maximum number of independently countable settlement signers.
    /// @dev Matches the creation and replacement bound fixed by TIP-1091.
    uint256 public constant MAX_SEQUENCERS = 8;

    /// @notice Duration of every emergency pause.
    uint64 public constant PAUSE_DURATION = 30 days;

    /// @notice Delay before a capability abdication becomes effective.
    uint64 public constant ABDICATION_DELAY = PAUSE_DURATION;

    bytes32 internal constant EIP712_DOMAIN_TYPEHASH = keccak256(
        "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
    );
    bytes32 internal constant NAME_HASH = keccak256("ZonePortal");
    bytes32 internal constant VERSION_HASH = keccak256("1");
    bytes32 internal constant SETTLEMENT_ATTESTATION_TYPEHASH = keccak256(
        "SettlementAttestation(uint32 zoneId,uint64 sequencerSetVersion,uint256 zoneHeight,uint256 withdrawalBatchIndex,address verifier,uint64 tempoBlockNumber,uint64 anchorBlockNumber,bytes32 anchorBlockHash,bytes32 blockTransitionHash,bytes32 depositQueueTransitionHash,bytes32 withdrawalQueueHash,bytes32 verifierConfigHash)"
    );
    /*//////////////////////////////////////////////////////////////
                                STORAGE
    //////////////////////////////////////////////////////////////*/

    /// @notice Governance admin address
    address public admin;

    /// @notice Zone gas rate (zone token units per gas unit on the zone)
    /// @dev Sequencer publishes this rate and takes the risk on zone gas costs.
    ///      Deposit fee = FIXED_DEPOSIT_GAS * zoneGasRate
    uint128 public zoneGasRate;
    uint64 public withdrawalBatchIndex;
    bytes32 public blockHash;

    /// @notice Current deposit queue hash (where new deposits land)
    bytes32 public currentDepositQueueHash;

    /// @notice Total number of deposits enqueued (monotonic counter, 1-indexed).
    /// @dev Each deposit(), depositEncrypted(), and withdrawal bounce-back increments this.
    ///      The deposit number is emitted in deposit events so users can track their position.
    uint64 public depositCount;

    /// @notice Last deposit number confirmed as processed by a batch proof.
    /// @dev Updated in submitBatch(). A deposit with number N is confirmed once
    ///      lastProcessedDepositNumber >= N.
    uint64 public lastProcessedDepositNumber;

    /// @notice Last Tempo block number the zone has synced to
    uint64 public lastSyncedTempoBlockNumber;

    /// @notice Gas amount used to price a failed-deposit bounce-back on Tempo.
    /// @dev Packed into the unused bytes in slot 4. Defaults to zero.
    uint64 public bouncebackGas;

    /// @notice Historical encryption public keys with activation blocks
    /// @dev Users specify which key they encrypted to (by index). Maintained for key rotation.
    ///      Stored at slot 5 in the ZonePortal storage layout.
    EncryptionKeyEntry[] internal _encryptionKeys;

    /// @notice Per-token configuration (stored at slot 6)
    /// @dev TokenConfig.enabled is permanent (write-once true); depositsActive can be toggled.
    mapping(address => TokenConfig) internal _tokenConfigs;

    /// @notice Append-only list of enabled tokens (stored at slot 7)
    /// @dev Tokens can never be removed from this list (non-custodial guarantee).
    address[] internal _enabledTokens;

    /// @notice Refunds parked after a deposit bounce-back transfer reverts on Tempo.
    mapping(address token => mapping(address owner => uint128 amount)) public refunds;

    /// @notice Withdrawal queue (zone→Tempo): unbounded FIFO
    WithdrawalQueue internal _withdrawalQueue;

    /// @notice Operator RPC endpoint for the zone
    string public rpcUrl;

    /// @notice Pending admin for two-step admin transfer
    address public pendingAdmin;

    /// @notice Reentrancy guard for withdrawal delivery.
    uint256 internal _withdrawalReentrancyStatus;

    /// @notice Zone metadata stored after the cross-domain layout.
    /// @dev These values must remain in account storage so each delegatecall proxy observes its
    ///      own metadata. Keep them after the established slots read directly by zone contracts.
    uint32 public zoneId;
    /// @notice Fixed callback messenger assigned during initialization.
    address public messenger;
    address public verifier;
    bool internal _initialized;

    /// @notice Configuration nonce for the active sequencer set and threshold.
    uint64 public sequencerSetVersion;
    uint8 public sequencerThreshold;
    uint256 public zoneHeight;
    address[] internal _sequencers;
    /// @dev Reserved slot 19, available for future use.
    uint256 private _reservedSlot19;
    /// @dev Mutually exclusive Portal roles. Sequencer membership is derived from this mapping.
    mapping(address => Role) internal role;

    /// @dev Solidity packs both enforcement booleans into slot 21.
    bool internal _isAccessEnforced;
    bool internal _isGatewayEnforced;

    /// @dev Reserve the remainder of slot 21 so the cross-domain fee cap has a dedicated slot.
    uint240 private _enforcementModesPadding;

    /// @notice Maximum Tempo gas rate a sequencer may configure on the zone-side outbox.
    /// @dev Defaults to zero and is read from finalized Tempo state by zone system contracts.
    uint128 public maxTempoGasRate;

    /// @notice Individual sequencer address of the active block-producing leader.
    /// @dev Appended after maxTempoGasRate; do not reorder existing storage. Zone nodes derive
    ///      leadership exclusively from finalized reads of these fields and the LeaderUpdated
    ///      event. Reads as zero for portals initialized before leadership landed; the first
    ///      setLeader from that state bootstraps epoch 1.
    address public leader;

    /// @notice Monotonic fencing epoch, incremented exactly once per real leader change.
    uint64 public leaderEpoch;

    /// @notice Tempo block number that recorded the most recent leader transition.
    uint64 public leaderActivationTempoBlock;

    /// @dev Per-Tempo-block deposit admission counter. Appended for upgrade-safe storage layout.
    uint64 internal _depositCountBlock;
    uint64 internal _depositsInCurrentBlock;

    /// @dev Per-Tempo-block token-enablement admission counter. Appended for upgrade safety.
    uint64 internal _tokenEnableCountBlock;
    uint64 internal _tokensEnabledInCurrentBlock;

    /// @notice Timestamp at which the current emergency pause expires.
    /// @dev Packed after the token-enablement counter in slot 25.
    uint64 public pauseExpiry;

    /// @notice Append-only commitment to every enabled token and its metadata.
    /// @dev Stored at slot 26.
    bytes32 public tokenEnablementHash;

    /// @notice Time after which the corresponding configuration surface is permanently closed.
    mapping(Capability => uint64) public abdicationEffectiveAt;

    /*//////////////////////////////////////////////////////////////
                             INITIALIZATION
    //////////////////////////////////////////////////////////////*/

    function initialize(
        uint32 _zoneId,
        address _initialToken,
        bool accessEnforced,
        bool gatewayEnforced,
        address[] calldata _allowedAccounts,
        address[] calldata _zoneGateways,
        address _messenger,
        address _admin,
        address[] calldata initialSequencers,
        uint8 _threshold,
        address _verifier,
        string calldata _rpcUrl
    )
        external
        onlyDelegateCall
    {
        if (msg.sender != ZONE_FACTORY_ADDRESS) revert NotFactory();
        if (_initialized) revert AlreadyInitialized();

        _initialized = true;
        zoneId = _zoneId;
        messenger = _messenger;
        admin = _admin;
        verifier = _verifier;
        _isAccessEnforced = accessEnforced;
        _isGatewayEnforced = gatewayEnforced;
        rpcUrl = _rpcUrl;
        emit EnforcementModesUpdated(accessEnforced, gatewayEnforced);

        _replaceSequencerSet(initialSequencers, _threshold, false);
        // The first sequencer bootstraps leadership so a fresh zone has a producer without a
        // separate setLeader call. The creation block is replayed by every zone node because
        // zone genesis anchors before createZone.
        _setLeader(initialSequencers[0]);

        for (uint256 i; i < _zoneGateways.length; ++i) {
            address account = _zoneGateways[i];
            require(role[account] == Role.None);
            role[account] = Role.CallbackGateway;
            emit RoleUpdated(account, Role.None, Role.CallbackGateway);
        }
        for (uint256 i; i < _allowedAccounts.length; ++i) {
            address account = _allowedAccounts[i];
            require(account != _messenger);
            require(role[account] == Role.None);
            role[account] = Role.Account;
            emit RoleUpdated(account, Role.None, Role.Account);
        }

        // Enable the initial token
        _enableTokenInternal(_initialToken);
    }

    /*//////////////////////////////////////////////////////////////
                               MODIFIERS
    //////////////////////////////////////////////////////////////*/

    /// @dev Initialization is valid only in a portal proxy's storage context.
    modifier onlyDelegateCall() {
        if (address(this) == ZONE_PORTAL_IMPL_ADDRESS) revert MustDelegateCall();
        _;
    }

    modifier onlySequencer() {
        if (!isSequencer(msg.sender)) revert NotSequencer();
        _;
    }

    modifier onlySequencerOrAdmin() {
        if (msg.sender != admin && !isSequencer(msg.sender)) revert NotSequencer();
        _;
    }

    modifier onlyAdmin() {
        if (msg.sender != admin) revert NotAdmin();
        _;
    }

    modifier whenNotPaused() {
        if (paused()) revert PortalIsPaused();
        _;
    }

    modifier onlySelf() {
        if (msg.sender != address(this)) revert NotSelf();
        _;
    }

    modifier nonReentrantWithdrawal() {
        if (_withdrawalReentrancyStatus != 0) revert ReentrantWithdrawal();
        _withdrawalReentrancyStatus = 1;
        _;
        _withdrawalReentrancyStatus = 0;
    }

    /// @inheritdoc IZonePortal
    function setSequencerSet(
        address[] calldata newSequencers,
        uint8 newThreshold
    )
        external
        onlyAdmin
    {
        _replaceSequencerSet(newSequencers, newThreshold, true);
    }

    function _replaceSequencerSet(
        address[] calldata newSequencers,
        uint8 newThreshold,
        bool rejectUnchanged
    )
        internal
    {
        uint256 length = newSequencers.length;
        if (length == 0 || length > MAX_SEQUENCERS || newThreshold == 0 || newThreshold > length) {
            revert InvalidSequencerSet();
        }

        for (uint256 i = 0; i < length; ++i) {
            address signer = newSequencers[i];
            if (signer == address(0)) revert InvalidSequencerSet();
            Role existing = role[signer];
            require(existing == Role.None || existing == Role.Sequencer);

            for (uint256 j = 0; j < i; ++j) {
                if (newSequencers[j] == signer) revert InvalidSequencerSet();
            }
        }

        bool membersUnchanged = length == _sequencers.length;
        if (membersUnchanged) {
            for (uint256 i = 0; i < length; ++i) {
                if (!isSequencer(newSequencers[i])) {
                    membersUnchanged = false;
                    break;
                }
            }
        }
        if (rejectUnchanged && membersUnchanged && newThreshold == sequencerThreshold) {
            revert SequencerConfigurationUnchanged();
        }

        for (uint256 i = 0; i < _sequencers.length; ++i) {
            address signer = _sequencers[i];
            role[signer] = Role.None;
            emit RoleUpdated(signer, Role.Sequencer, Role.None);
        }
        delete _sequencers;
        for (uint256 i = 0; i < length; ++i) {
            address signer = newSequencers[i];
            _sequencers.push(signer);
            role[signer] = Role.Sequencer;
            emit RoleUpdated(signer, Role.None, Role.Sequencer);
        }
        // Rotating out the active leader would strand block production: transfer leadership
        // first (add the replacement, setLeader, then remove the old member).
        if (leader != address(0) && !isSequencer(leader)) {
            revert ActiveLeaderRemoved();
        }

        sequencerThreshold = newThreshold;
        uint64 nonce = sequencerSetVersion;
        if (rejectUnchanged) nonce = ++sequencerSetVersion;
        emit SequencerSetUpdated(nonce, newThreshold, newSequencers);
    }

    /// @inheritdoc IZonePortal
    function sequencerCount() external view returns (uint256) {
        return _sequencers.length;
    }

    /// @inheritdoc IZonePortal
    function sequencerAt(uint256 index) external view returns (address) {
        return _sequencers[index];
    }

    /// @inheritdoc IZonePortal
    function isSequencer(address account) public view returns (bool) {
        return role[account] == Role.Sequencer;
    }

    /// @inheritdoc IZonePortal
    function setLeader(address newLeader, uint64 expectedEpoch) external onlySequencerOrAdmin {
        if (!isSequencer(newLeader)) revert InvalidLeader();
        // Idempotent fanout: every node relays the same target, only the first call transitions.
        if (newLeader == leader) return;
        // Compare-and-set: a delayed duplicate carrying a pre-handoff epoch cannot roll
        // leadership back after a later transition.
        if (leaderEpoch != expectedEpoch) {
            revert StaleLeadershipEpoch(expectedEpoch, leaderEpoch);
        }
        // One distinct leader per Tempo block keeps exactly one authorized producer for the
        // corresponding zone block.
        if (leaderActivationTempoBlock == uint64(block.number)) {
            revert LeaderAlreadyUpdatedThisBlock();
        }

        _setLeader(newLeader);
    }

    /// @dev Single write path for a leadership transition: assign, bump the fencing epoch,
    ///      stamp the activation block, emit. `crates/l1` decodes `LeaderUpdated` to drive
    ///      node roles, so every transition must go through here to stay consistent.
    function _setLeader(address newLeader) private {
        address previous = leader;
        leader = newLeader;
        leaderEpoch += 1;
        uint64 activationTempoBlock = uint64(block.number);
        leaderActivationTempoBlock = activationTempoBlock;
        emit LeaderUpdated(previous, newLeader, leaderEpoch, activationTempoBlock);
    }

    /// @notice Set zone gas rate. Only callable by admin.
    /// @dev The admin publishes the operational rate and receives collected deposit fees.
    /// @param _zoneGasRate Zone token units per gas unit on the zone
    function setZoneGasRate(uint128 _zoneGasRate) external onlyAdmin {
        if (_zoneGasRate > MAX_GAS_FEE_RATE) revert GasFeeRateTooHigh();
        zoneGasRate = _zoneGasRate;
        emit ZoneGasRateUpdated(_zoneGasRate);
    }

    /// @notice Set the maximum Tempo gas rate a sequencer may configure on the zone-side outbox.
    function setMaxTempoGasRate(uint128 _maxTempoGasRate) external onlyAdmin {
        if (_maxTempoGasRate > MAX_GAS_FEE_RATE) revert GasFeeRateTooHigh();
        maxTempoGasRate = _maxTempoGasRate;
        emit MaxTempoGasRateUpdated(_maxTempoGasRate);
    }

    /// @notice Set the gas amount used to price failed-deposit bounce-backs on Tempo.
    /// @dev Only the admin can change the amount because it determines the fee deducted from a
    ///      failed deposit at processing time.
    function setBouncebackGas(uint64 _bouncebackGas) external onlyAdmin {
        bouncebackGas = _bouncebackGas;
        emit BouncebackGasUpdated(_bouncebackGas);
    }

    /*//////////////////////////////////////////////////////////////
                             ADMIN MANAGEMENT
    //////////////////////////////////////////////////////////////*/

    /// @notice Start an admin transfer. Only callable by the current admin.
    /// @dev Two-step handoff: the new admin only takes over once it calls
    ///      {acceptAdmin}, which prevents fat-fingered transfers.
    ///      Passing address(0) cancels a pending transfer.
    /// @param newAdmin The address that will become admin after accepting (address(0) cancels).
    function transferAdmin(address newAdmin) external onlyAdmin {
        pendingAdmin = newAdmin;
        emit AdminTransferStarted(admin, newAdmin);
    }

    /// @notice Accept a pending admin transfer. Only callable by the pending admin.
    /// @dev The explicit `pendingAdmin == address(0)` check because it is technically
    ///      possible to make a system tx on L1 with msg.sender == 0.
    ///      The Admin key can only be rotated, never renounced.
    function acceptAdmin() external {
        if (pendingAdmin == address(0) || msg.sender != pendingAdmin) revert NotPendingAdmin();
        address previousAdmin = admin;
        admin = pendingAdmin;
        pendingAdmin = address(0);
        emit AdminTransferred(previousAdmin, admin);
    }

    /// @notice Enable or disable account allowlist enforcement without discarding membership.
    function setAccessMode(bool enforced) external onlyAdmin {
        _requireCapabilityActive(Capability.AccessPolicy);
        _isAccessEnforced = enforced;
        emit EnforcementModesUpdated(enforced, _isGatewayEnforced);
    }

    /// @notice Enable or disable callback gateway registration enforcement.
    function setGatewayMode(bool enforced) external onlyAdmin {
        _requireCapabilityActive(Capability.AccessPolicy);
        _isGatewayEnforced = enforced;
        emit EnforcementModesUpdated(_isAccessEnforced, enforced);
    }

    /// @notice Return whether account allowlist enforcement is enabled.
    function isAccessEnforced() public view returns (bool) {
        return _isAccessEnforced;
    }

    /// @notice Return whether callback gateway registration enforcement is disabled.
    function isGatewayOpen() public view returns (bool) {
        return !_isGatewayEnforced;
    }

    /// @notice Add or remove an account from closed-loop portal flows.
    /// @dev Returns without emitting when already configured. Abdication freezes all changes.
    function setAllowedAccount(address account, bool allowed) external onlyAdmin {
        _requireCapabilityActive(Capability.AccessPolicy);
        if (allowed) require(account != messenger);
        Role previous = role[account];
        Role next = allowed ? Role.Account : Role.None;
        if (previous == next) return;
        require(previous == (allowed ? Role.None : Role.Account));
        role[account] = next;
        emit RoleUpdated(account, previous, next);
    }

    /// @notice Add or remove a callback gateway.
    /// @dev Returns without emitting when already configured. Abdication freezes all changes.
    function setGateway(address account, bool allowed) external onlyAdmin {
        _requireCapabilityActive(Capability.AccessPolicy);
        Role previous = role[account];
        Role next = allowed ? Role.CallbackGateway : Role.None;
        if (previous == next) return;
        require(previous == (allowed ? Role.None : Role.CallbackGateway));
        role[account] = next;
        emit RoleUpdated(account, previous, next);
    }

    /// @notice Add or remove a pause guardian.
    /// @dev Pause-capability abdication freezes both additions and removals.
    function setPauseGuardian(address account, bool allowed) external onlyAdmin {
        _requireCapabilityActive(Capability.PausePortal);
        Role previous = role[account];
        Role next = allowed ? Role.PauseGuardian : Role.None;
        if (previous == next) return;
        require(previous == (allowed ? Role.None : Role.PauseGuardian));
        role[account] = next;
        emit RoleUpdated(account, previous, next);
    }

    function hasRole(address account, Role expected) public view returns (bool) {
        return role[account] == expected;
    }

    /*//////////////////////////////////////////////////////////////
                           QUEUE ACCESSORS
    //////////////////////////////////////////////////////////////*/

    function withdrawalQueueHead() external view returns (uint256) {
        return _withdrawalQueue.head;
    }

    function withdrawalQueueTail() external view returns (uint256) {
        return _withdrawalQueue.tail;
    }

    function withdrawalQueueSlot(uint256 queueIndex) external view returns (bytes32) {
        return _withdrawalQueue.slots[queueIndex];
    }

    /*//////////////////////////////////////////////////////////////
                          TOKEN REGISTRY
    //////////////////////////////////////////////////////////////*/

    /// @notice Check if a token is enabled for bridging
    function isTokenEnabled(address _token) external view returns (bool) {
        return _tokenConfigs[_token].enabled;
    }

    /// @notice Check if deposits are currently active for a token
    function areDepositsActive(address _token) external view returns (bool) {
        TokenConfig storage cfg = _tokenConfigs[_token];
        return !paused() && cfg.enabled && cfg.depositsActive;
    }

    /// @notice Get the token configuration for a specific token
    function tokenConfig(address _token) external view returns (TokenConfig memory) {
        return _tokenConfigs[_token];
    }

    /// @notice Get the number of enabled tokens
    function enabledTokenCount() external view returns (uint256) {
        return _enabledTokens.length;
    }

    /// @notice Get an enabled token by index
    function enabledTokenAt(uint256 index) external view returns (address) {
        return _enabledTokens[index];
    }

    /// @notice Whether deposits and withdrawal processing are currently paused.
    /// @dev The pause expires automatically once block.timestamp reaches pauseExpiry.
    function paused() public view returns (bool) {
        return block.timestamp < pauseExpiry;
    }

    /// @notice Pause deposits and withdrawal processing for 30 days.
    function pause() external whenNotPaused {
        _requireCapabilityActive(Capability.PausePortal);
        if (
            msg.sender != admin && !isSequencer(msg.sender)
                && !hasRole(msg.sender, Role.PauseGuardian)
        ) {
            revert NotPauseAuthority();
        }
        pauseExpiry = uint64(block.timestamp) + PAUSE_DURATION;
        emit PortalPaused(msg.sender);
    }

    /// @notice Resume deposits and withdrawal processing before the pause expires.
    /// @dev Admin recovery remains available after the pause capability is abdicated.
    function resume() external onlyAdmin {
        pauseExpiry = 0;
        emit PortalResumed(msg.sender);
    }

    /// @notice Schedule permanent abdication of a Portal configuration surface.
    function abdicate(Capability capability) external onlyAdmin whenNotPaused {
        if (abdicationEffectiveAt[capability] != 0) revert AbdicationAlreadyScheduled(capability);
        uint64 effectiveAt = uint64(block.timestamp) + ABDICATION_DELAY;
        abdicationEffectiveAt[capability] = effectiveAt;
        emit AbdicationScheduled(capability, effectiveAt);
    }

    function _requireCapabilityActive(Capability capability) internal view {
        uint64 effectiveAt = abdicationEffectiveAt[capability];
        if (effectiveAt != 0 && block.timestamp >= effectiveAt) {
            revert CapabilityAbdicated(capability);
        }
    }

    /// @notice Enable a new TIP-20 token for bridging. Only callable by admin.
    /// @dev Irreversible: once enabled, a token cannot be disabled.
    function enableToken(address _token) external onlyAdmin {
        if (_tokenConfigs[_token].enabled) revert TokenAlreadyEnabled();
        if (!ITIP20Factory(StdPrecompiles.TIP20_FACTORY_ADDRESS).isTIP20(_token)) {
            revert TokenNotEnabled();
        }
        _enableTokenInternal(_token);
    }

    /// @notice Pause deposits for a token. Only callable by admin.
    /// @dev Does not affect withdrawal processing (non-custodial guarantee).
    function pauseDeposits(address _token) external onlyAdmin {
        if (!_tokenConfigs[_token].enabled) revert TokenNotEnabled();
        _tokenConfigs[_token].depositsActive = false;
        emit DepositsPaused(_token);
    }

    /// @notice Resume deposits for a token. Only callable by admin.
    function resumeDeposits(address _token) external onlyAdmin {
        if (!_tokenConfigs[_token].enabled) revert TokenNotEnabled();
        _tokenConfigs[_token].depositsActive = true;
        emit DepositsResumed(_token);
    }

    /// @notice Internal function to enable a token (used by initializer and enableToken)
    function _enableTokenInternal(address _token) internal {
        _recordTokenEnablement();

        // Bound the metadata copied into the zone before mutating portal or policy state. The zone
        // must initialize every token emitted in this block inside advanceTempo's fixed gas budget.
        string memory name = ITIP20(_token).name();
        string memory symbol = ITIP20(_token).symbol();
        string memory currency = ITIP20(_token).currency();
        if (
            bytes(name).length > MAX_TOKEN_METADATA_BYTES
                || bytes(symbol).length > MAX_TOKEN_METADATA_BYTES
                || bytes(currency).length > MAX_TOKEN_METADATA_BYTES
        ) {
            revert TokenMetadataTooLong();
        }

        address[] memory tokens = new address[](1);
        tokens[0] = _token;

        (bool isSet,) = TIP403_REGISTRY.tokenTransferPolicyId(_token);
        if (!isSet) {
            TIP403_REGISTRY.migrateTransferPolicyIds(tokens);
            (isSet,) = TIP403_REGISTRY.tokenTransferPolicyId(_token);
        }
        if (!isSet) {
            revert TokenTransferPolicyNotSet();
        }

        tokenEnablementHash =
            keccak256(abi.encode(tokenEnablementHash, _token, name, symbol, currency));
        _tokenConfigs[_token] = TokenConfig({ enabled: true, depositsActive: true });
        _enabledTokens.push(_token);

        emit TokenEnabled(_token, name, symbol, currency);
    }

    function _recordTokenEnablement() internal {
        uint64 currentBlock = uint64(block.number);
        if (_tokenEnableCountBlock != currentBlock) {
            _tokenEnableCountBlock = currentBlock;
            _tokensEnabledInCurrentBlock = 0;
        }
        if (_tokensEnabledInCurrentBlock >= MAX_TOKENS_ENABLED_PER_TEMPO_BLOCK) {
            revert TokenEnablementBlockCapacityExceeded(MAX_TOKENS_ENABLED_PER_TEMPO_BLOCK);
        }
        unchecked {
            ++_tokensEnabledInCurrentBlock;
        }
    }

    /// @notice Update the zone's operator RPC endpoint.
    /// @param _rpcUrl The new RPC url
    function setRpcUrl(string calldata _rpcUrl) external onlySequencer {
        rpcUrl = _rpcUrl;
        emit RpcUrlUpdated(_rpcUrl);
    }

    /*//////////////////////////////////////////////////////////////
                        ENCRYPTION KEY MANAGEMENT
    //////////////////////////////////////////////////////////////*/

    /// @notice Get the sequencer's current encryption public key
    /// @return x The X coordinate
    /// @return yParity The Y coordinate parity (0x02 or 0x03)
    /// @return pubkey The address derived from the public key
    function sequencerEncryptionKey()
        external
        view
        returns (bytes32 x, uint8 yParity, address pubkey)
    {
        if (_encryptionKeys.length == 0) revert NoEncryptionKeySet();
        EncryptionKeyEntry storage current = _encryptionKeys[_encryptionKeys.length - 1];
        return (current.x, current.yParity, Secp256k1Lib.deriveAddress(current.x, current.yParity));
    }

    /// @notice Set the sequencer's encryption public key with proof of possession from its private key
    /// @dev Only callable by an active sequencer or the admin. Appends to key history.
    ///      No reentrancy guard is needed because this function makes no unrestricted external
    ///      calls; its only external calls are to fixed cryptographic precompiles.
    ///      Requires a valid ECDSA signature over keccak256(abi.encode(address(this), x, yParity))
    ///      produced by the private key corresponding to (x, yParity). This prevents accidental
    ///      registration of keys the sequencer cannot decrypt with.
    /// @param x The X coordinate (must be valid secp256k1 point)
    /// @param yParity The Y coordinate parity (0x02 or 0x03)
    /// @param popV Recovery id of the proof-of-possession signature
    /// @param popR R component of the proof-of-possession signature
    /// @param popS S component of the proof-of-possession signature
    function setSequencerEncryptionKey(
        bytes32 x,
        uint8 yParity,
        uint8 popV,
        bytes32 popR,
        bytes32 popS
    )
        external
        onlySequencerOrAdmin
    {
        // Validate yParity
        if (!Secp256k1Lib.isCompressedYParity(yParity)) revert InvalidEphemeralPubkey();

        // Validate x is on the secp256k1 curve
        if (!Secp256k1Lib.isValidX(x)) revert InvalidEphemeralPubkey();

        // Verify proof of possession: the caller must prove control of the encryption private key.
        bytes32 message = keccak256(abi.encode(address(this), x, yParity));
        address recovered = ecrecover(message, popV, popR, popS);
        address expected = Secp256k1Lib.deriveAddress(x, yParity);
        if (recovered == address(0) || recovered != expected) {
            revert InvalidProofOfPossession();
        }

        uint64 activationBlock = uint64(block.number);
        _encryptionKeys.push(
            EncryptionKeyEntry({ x: x, yParity: yParity, activationBlock: activationBlock })
        );
        emit SequencerEncryptionKeyUpdated(
            x, yParity, expected, _encryptionKeys.length - 1, activationBlock
        );
    }

    /// @notice Get the number of keys in the history
    function encryptionKeyCount() external view returns (uint256) {
        return _encryptionKeys.length;
    }

    /// @notice Get a historical encryption key by index
    /// @param index The index in the key history (0 = first key)
    /// @return entry The key entry with activation block
    function encryptionKeyAt(uint256 index)
        external
        view
        returns (EncryptionKeyEntry memory entry)
    {
        if (index >= _encryptionKeys.length) {
            revert InvalidEncryptionKeyIndex(index);
        }
        return _encryptionKeys[index];
    }

    /// @notice Get the encryption key that was active at a specific Tempo block
    /// @dev Binary search through key history to find the correct key
    /// @param tempoBlockNumber The Tempo block number to query
    /// @return x The X coordinate of the active key
    /// @return yParity The Y coordinate parity
    /// @return keyIndex The index of this key in history
    function encryptionKeyAtBlock(uint64 tempoBlockNumber)
        external
        view
        returns (bytes32 x, uint8 yParity, uint256 keyIndex)
    {
        uint256 len = _encryptionKeys.length;
        if (len == 0 || tempoBlockNumber < _encryptionKeys[0].activationBlock) {
            revert NoEncryptionKeyAtBlock(tempoBlockNumber);
        }

        uint256 low = 0;
        uint256 high = len - 1;
        while (low < high) {
            uint256 mid = (low + high + 1) / 2;
            if (_encryptionKeys[mid].activationBlock <= tempoBlockNumber) {
                low = mid;
            } else {
                high = mid - 1;
            }
        }

        EncryptionKeyEntry storage entry = _encryptionKeys[low];
        return (entry.x, entry.yParity, low);
    }

    /// @notice Check if an encryption key is still valid for new deposits
    /// @param keyIndex The key index to check
    /// @return valid True if the key can be used for new deposits
    /// @return expiresAtBlock Block number when this key expires (0 if current key)
    function isEncryptionKeyValid(uint256 keyIndex)
        public
        view
        returns (bool valid, uint64 expiresAtBlock)
    {
        if (keyIndex >= _encryptionKeys.length) {
            return (false, 0);
        }

        // Current key (latest) never expires
        if (keyIndex == _encryptionKeys.length - 1) {
            return (true, 0);
        }

        // Old keys are valid during grace period after supersession
        EncryptionKeyEntry storage nextKey = _encryptionKeys[keyIndex + 1];
        uint64 expiration = nextKey.activationBlock + ENCRYPTION_KEY_GRACE_PERIOD;

        valid = block.number < expiration;
        expiresAtBlock = expiration;
    }

    /*//////////////////////////////////////////////////////////////
                               DEPOSITS
    //////////////////////////////////////////////////////////////*/

    /// @notice Calculate the fee for a deposit
    /// @dev Fee = FIXED_DEPOSIT_GAS * zoneGasRate
    /// @return fee The deposit fee in zone token units
    function calculateDepositFee() public view returns (uint128 fee) {
        fee = uint128(FIXED_DEPOSIT_GAS) * zoneGasRate;
    }

    /// @notice Calculate the reserved fee for a failed-deposit bounce-back
    /// @dev Fee = ceil(bouncebackGas * block.basefee / 1e12)
    /// @return fee The bounce-back fee in token units
    function calculateBouncebackFee() public view returns (uint128 fee) {
        uint256 gasFee = uint256(bouncebackGas) * block.basefee;
        // Round up after scaling so bounce-backs do not underpay.
        fee = uint128((gasFee + TEMPO_BASE_FEE_SCALE - 1) / TEMPO_BASE_FEE_SCALE);
    }

    function _validateDepositsActive(address _token) internal view {
        TokenConfig storage cfg = _tokenConfigs[_token];
        if (!cfg.enabled) revert TokenNotEnabled();
        if (!cfg.depositsActive) revert DepositsNotActive();
    }

    function _requireAllowed(address account) internal view {
        if (!_isAllowed(account)) revert AccountNotAllowed(account);
    }

    function _requireAllowedDepositor(address account) internal view {
        if (!_isAccessEnforced) return;
        if (_isGatewayEnforced && hasRole(account, Role.CallbackGateway)) {
            return;
        }
        if (!hasRole(account, Role.Account)) revert AccountNotAllowed(account);
    }

    function _isAllowed(address account) internal view returns (bool) {
        return !_isAccessEnforced || hasRole(account, Role.Account);
    }

    function _collectDepositFunds(
        address _token,
        uint128 amount
    )
        internal
        returns (uint128 fee, uint128 netAmount)
    {
        fee = calculateDepositFee();
        uint128 bouncebackFee = calculateBouncebackFee();
        if (amount < fee + bouncebackFee) revert DepositTooSmall();
        netAmount = amount - fee;

        // TIP-20 transfers revert on failure, so no boolean check is needed here.
        ITIP20(_token).transferFrom(msg.sender, address(this), amount);
        if (fee > 0) {
            ITIP20(_token).transfer(admin, fee);
        }
    }

    function _recordDeposit(
        bytes32 newCurrentDepositQueueHash,
        uint64 maximum
    )
        internal
        returns (uint64 thisDeposit)
    {
        uint64 currentBlock = uint64(block.number);
        if (_depositCountBlock != currentBlock) {
            _depositCountBlock = currentBlock;
            _depositsInCurrentBlock = 0;
        }
        if (_depositsInCurrentBlock >= maximum) {
            revert DepositBlockCapacityExceeded(maximum);
        }
        unchecked {
            ++_depositsInCurrentBlock;
        }

        currentDepositQueueHash = newCurrentDepositQueueHash;
        thisDeposit = ++depositCount;
    }

    /// @notice Alias for `depositEncrypted`.
    function deposit(
        address _token,
        uint128 amount,
        uint256 keyIndex,
        DepositPayload calldata encrypted,
        address tempoRefundRecipient
    )
        external
        whenNotPaused
        returns (bytes32 newCurrentDepositQueueHash)
    {
        return _deposit(_token, amount, keyIndex, encrypted, tempoRefundRecipient);
    }

    /// @notice Deposit with encrypted recipient and memo
    /// @dev The encrypted payload contains (to, memo) encrypted to the sequencer's key.
    ///      The token identity is public (not encrypted) since the portal must escrow it.
    ///      Validates that keyIndex is valid (exists and not expired).
    ///      Charges the configured zone deposit fee.
    /// @param _token The TIP-20 token to deposit
    /// @param amount Amount to deposit (fee deducted from this amount)
    /// @param keyIndex Index of the encryption key used (from encryptionKeyAt)
    /// @param encrypted The encrypted payload (recipient and memo)
    /// @return newCurrentDepositQueueHash The new deposit queue hash
    function depositEncrypted(
        address _token,
        uint128 amount,
        uint256 keyIndex,
        DepositPayload calldata encrypted,
        address tempoRefundRecipient
    )
        public
        whenNotPaused
        returns (bytes32 newCurrentDepositQueueHash)
    {
        return _deposit(_token, amount, keyIndex, encrypted, tempoRefundRecipient);
    }

    function _deposit(
        address _token,
        uint128 amount,
        uint256 keyIndex,
        DepositPayload calldata encrypted,
        address tempoRefundRecipient
    )
        internal
        returns (bytes32 newCurrentDepositQueueHash)
    {
        if (tempoRefundRecipient == address(0)) revert InvalidBouncebackRecipient();
        // Enforced gateways may deposit callback returns without also being allowed accounts.
        _requireAllowedDepositor(msg.sender);
        _requireAllowed(tempoRefundRecipient);

        _validateDepositsActive(_token);

        uint64 policyId = ITIP20(_token).transferPolicyId();
        if (!TIP403_REGISTRY.isAuthorizedRecipient(policyId, tempoRefundRecipient)) {
            revert ITIP20.PolicyForbids();
        }

        // Validate ephemeral public key is a valid secp256k1 point
        // Prevents griefing: invalid points make Chaum-Pedersen proofs impossible,
        // which would block chain progress on the zone side.
        if (!Secp256k1Lib.isCompressedYParity(encrypted.ephemeralPubkeyYParity)) {
            revert InvalidEphemeralPubkey();
        }
        if (!Secp256k1Lib.isValidX(encrypted.ephemeralPubkeyX)) {
            revert InvalidEphemeralPubkey();
        }

        // Validate ciphertext length — GCM ciphertext == plaintext length (tag is separate)
        // Prevents DoS: oversized ciphertexts inflate zone-side AES-GCM processing cost
        if (encrypted.ciphertext.length != ENCRYPTED_PAYLOAD_PLAINTEXT_SIZE) {
            revert InvalidCiphertextLength(
                encrypted.ciphertext.length, ENCRYPTED_PAYLOAD_PLAINTEXT_SIZE
            );
        }

        // Validate encryption key
        (bool valid,) = isEncryptionKeyValid(keyIndex);
        if (!valid) {
            if (keyIndex >= _encryptionKeys.length) {
                revert InvalidEncryptionKeyIndex(keyIndex);
            }
            EncryptionKeyEntry storage key = _encryptionKeys[keyIndex];
            EncryptionKeyEntry storage nextKey = _encryptionKeys[keyIndex + 1];
            revert EncryptionKeyExpired(keyIndex, key.activationBlock, nextKey.activationBlock);
        }

        (uint128 fee, uint128 netAmount) = _collectDepositFunds(_token, amount);

        // Build the queued deposit.
        Deposit memory depositData = Deposit({
            token: _token,
            sender: msg.sender,
            amount: netAmount,
            tempoRefundRecipient: tempoRefundRecipient,
            keyIndex: keyIndex,
            encrypted: encrypted
        });

        // Insert the deposit into the queue.
        newCurrentDepositQueueHash =
            DepositQueueLib.enqueueDeposit(currentDepositQueueHash, depositData);
        uint64 thisDeposit = _recordDeposit(
            newCurrentDepositQueueHash, MAX_DEPOSITS_PER_TEMPO_BLOCK - WITHDRAWAL_BOUNCEBACK_RESERVE
        );

        emit DepositMade(
            newCurrentDepositQueueHash,
            msg.sender,
            _token,
            netAmount,
            fee,
            keyIndex,
            encrypted.ephemeralPubkeyX,
            encrypted.ephemeralPubkeyYParity,
            encrypted.ciphertext,
            encrypted.nonce,
            encrypted.tag,
            tempoRefundRecipient,
            thisDeposit
        );
    }

    /*//////////////////////////////////////////////////////////////
                             WITHDRAWALS
    //////////////////////////////////////////////////////////////*/

    /// @notice Process multiple withdrawals from the queue in a single transaction.
    /// @dev Withdrawals must be supplied in queue order. `remainingQueue` is the queue suffix
    ///      after the last supplied withdrawal, or zero if the batch exhausts the current slot.
    ///      Plain-transfer and callback failures bounce back without blocking the FIFO.
    function processWithdrawals(
        Withdrawal[] calldata withdrawals,
        bytes32 remainingQueue
    )
        external
        onlySequencer
        whenNotPaused
        nonReentrantWithdrawal
    {
        bytes32[] memory remainingQueues = new bytes32[](withdrawals.length);
        bytes32 nextQueue = remainingQueue;

        for (uint256 i = withdrawals.length; i > 0; --i) {
            remainingQueues[i - 1] = nextQueue;
            nextQueue = keccak256(abi.encode(withdrawals[i - 1], nextQueue));
        }

        for (uint256 i; i < withdrawals.length; ++i) {
            _processWithdrawal(withdrawals[i], remainingQueues[i]);
        }
    }

    function _processWithdrawal(Withdrawal calldata withdrawal, bytes32 remainingQueue) internal {
        // Pop from withdrawal queue (library handles swap and hash verification)
        _withdrawalQueue.dequeue(withdrawal, remainingQueue);

        address _token = withdrawal.token;

        if (withdrawal.fallbackNonce == 0) {
            _processDepositBounceBack(withdrawal);
            return;
        }

        if (withdrawal.gasLimit > MAX_WITHDRAWAL_GAS_LIMIT) {
            _enqueueBounceBack(_token, withdrawal.amount, withdrawal.fallbackNonce);
            emit WithdrawalProcessed(
                withdrawal.to, withdrawal.senderTag, _token, withdrawal.amount, false
            );
            return;
        }

        bool success;
        if (withdrawal.gasLimit == 0) {
            // Re-check current roles without reverting so an in-flight withdrawal to a revoked
            // account or newly registered gateway bounces without blocking the FIFO.
            success = (!_isGatewayEnforced || !hasRole(withdrawal.to, Role.CallbackGateway))
                && _isAllowed(withdrawal.to)
                && _tryTransfer(_token, withdrawal.to, withdrawal.amount);
        } else {
            // Isolate callback effects so failure can be caught without reverting the dequeue.
            try this.deliverWithdrawal(
                _token,
                withdrawal.to,
                withdrawal.amount,
                withdrawal.senderTag,
                withdrawal.gasLimit,
                withdrawal.callbackData
            ) {
                success = true;
            } catch {
                success = false;
            }
        }

        if (!success) {
            _enqueueBounceBack(_token, withdrawal.amount, withdrawal.fallbackNonce);
        }
        emit WithdrawalProcessed(
            withdrawal.to, withdrawal.senderTag, _token, withdrawal.amount, success
        );
    }

    /// @notice Deliver a callback withdrawal in a revertable self-call frame.
    /// @dev Only callable by this portal. processWithdrawals catches failures and bounces back.
    function deliverWithdrawal(
        address token,
        address target,
        uint128 amount,
        bytes32 senderTag,
        uint64 gasLimit,
        bytes calldata data
    )
        external
        onlySelf
    {
        if (_isGatewayEnforced && !hasRole(target, Role.CallbackGateway)) {
            revert InvalidCallbackTarget();
        }
        if (!ITIP20(token).transfer(messenger, amount)) {
            revert TransferFailed();
        }

        bytes32 depositQueueHashBefore = currentDepositQueueHash;

        // We copy whatever the messenger reverts with, so keep its errors small.
        IZoneMessenger(messenger)
            .relayMessage(zoneId, token, senderTag, target, amount, gasLimit, data);

        // In closed access, this proves only that some deposit was appended to this portal; it does
        // not bind that deposit to the callback's token, amount, or recipient. Callback data is
        // opaque, so an enforced gateway is trusted to constrain the operation and return the
        // intended result. Open access imposes no source-deposit invariant: callback value may go
        // to another zone or leave the zone system entirely.
        if (_isAccessEnforced && currentDepositQueueHash == depositQueueHashBefore) {
            revert CallbackDidNotReturnToZone();
        }
    }

    function _processDepositBounceBack(Withdrawal calldata withdrawal) internal {
        address _token = withdrawal.token;
        uint128 bouncebackFee = calculateBouncebackFee();
        if (bouncebackFee > withdrawal.amount) {
            bouncebackFee = withdrawal.amount;
        }
        // Only deduct the fee if the admin transfer succeeds; otherwise the full amount remains
        // refundable to the deposit recipient.
        uint128 collectedFee;
        if (bouncebackFee > 0 && _tryTransfer(_token, admin, bouncebackFee)) {
            collectedFee = bouncebackFee;
        }
        uint128 refundAmount = withdrawal.amount - collectedFee;

        bool success =
            _isAllowed(withdrawal.to) && _tryTransfer(_token, withdrawal.to, refundAmount);

        if (success) {
            emit DepositBounceBack(withdrawal.to, _token, refundAmount, collectedFee);
        } else {
            refunds[_token][withdrawal.to] += refundAmount;
            emit DepositBounceBackPending(withdrawal.to, _token, refundAmount, collectedFee);
        }
    }

    function claimRefund(address token) external returns (uint128 amount) {
        _requireAllowed(msg.sender);
        amount = refunds[token][msg.sender];
        refunds[token][msg.sender] = 0;

        if (!_tryTransfer(token, msg.sender, amount)) revert CallbackRejected();

        emit RefundClaimed(msg.sender, token, amount);
    }

    /// @notice Attempt a TIP-20 transfer without bubbling recipient/policy reverts.
    /// @dev Returns false if the receive policy blocks direct delivery, or if the token transfer
    ///      reverts or returns false. Callers decide whether a failed transfer should be ignored,
    ///      parked for refund, or reverted.
    /// @param token The TIP-20 token to transfer.
    /// @param to The recipient address.
    /// @param amount The token amount to transfer.
    /// @return success True if the transfer completed directly to `to` and returned true.
    function _tryTransfer(
        address token,
        address to,
        uint128 amount
    )
        internal
        returns (bool success)
    {
        address effectiveRecipient;
        try StdPrecompiles.ADDRESS_REGISTRY.resolveRecipient(to) returns (address resolved) {
            effectiveRecipient = resolved;
        } catch {
            return false;
        }

        try TIP403_REGISTRY.validateReceivePolicy(
            token, address(this), effectiveRecipient
        ) returns (
            bool authorized, ITIP403Registry.BlockedReason
        ) {
            if (!authorized) return false;
        } catch {
            return false;
        }

        try ITIP20(token).transfer(to, amount) returns (bool ok) {
            return ok;
        } catch {
            return false;
        }
    }

    /// @notice Enqueue a bounce-back deposit for failed callback
    /// @param _token The token from the failed withdrawal
    /// @param amount The amount to bounce back
    /// @param fallbackNonce The nonce resolving to the zone bounce-back recipient
    function _enqueueBounceBack(address _token, uint128 amount, uint64 fallbackNonce) internal {
        WithdrawalBounceBackDeposit memory depositData = WithdrawalBounceBackDeposit({
            token: _token, to: address(uint160(fallbackNonce)), amount: amount
        });

        bytes32 newCurrentDepositQueueHash =
            DepositQueueLib.enqueue(currentDepositQueueHash, depositData);
        uint64 thisDeposit =
            _recordDeposit(newCurrentDepositQueueHash, MAX_DEPOSITS_PER_TEMPO_BLOCK);

        emit WithdrawalBounceBack(
            newCurrentDepositQueueHash, fallbackNonce, _token, amount, thisDeposit
        );
    }

    /*//////////////////////////////////////////////////////////////
                           BATCH SUBMISSION
    //////////////////////////////////////////////////////////////*/

    /// @inheritdoc IZonePortal
    function submitBatch(
        uint64 tempoBlockNumber,
        uint64 recentTempoBlockNumber,
        BlockTransition calldata blockTransition,
        DepositQueueTransition calldata depositQueueTransition,
        bytes32 withdrawalQueueHash,
        bytes calldata verifierConfig,
        bytes calldata proof,
        uint256 nextZoneHeight,
        bytes[] calldata signatures
    )
        external
        onlySequencer
    {
        if (blockTransition.prevBlockHash != blockHash) {
            revert InvalidProof();
        }

        // Determine anchor block: either tempoBlockNumber (direct) or recentTempoBlockNumber (ancestry)
        uint64 anchorBlockNumber;
        bytes32 anchorBlockHash;

        if (recentTempoBlockNumber == 0) {
            // Direct mode: read tempoBlockNumber hash from EIP-2935
            anchorBlockNumber = tempoBlockNumber;
            if (tempoBlockNumber > block.number) {
                revert InvalidTempoBlockNumber();
            }

            anchorBlockHash = getBlockHash(tempoBlockNumber);
        } else {
            // Ancestry mode: read recentTempoBlockNumber hash, proof verifies ancestry chain
            if (recentTempoBlockNumber <= tempoBlockNumber) {
                revert InvalidTempoBlockNumber();
            }
            if (recentTempoBlockNumber > block.number) {
                revert InvalidTempoBlockNumber();
            }

            anchorBlockNumber = recentTempoBlockNumber;
            anchorBlockHash = getBlockHash(recentTempoBlockNumber);
        }

        if (anchorBlockHash == bytes32(0)) revert InvalidTempoBlockNumber();

        // The certificate binds every value that affects settlement, rather than only the
        // zone block hash. A leader therefore cannot reuse signatures for this block with a
        // different withdrawal root, deposit transition, Tempo anchor, or verifier config.
        if (!_verifySettlement(
                nextZoneHeight,
                tempoBlockNumber,
                anchorBlockNumber,
                anchorBlockHash,
                blockTransition,
                depositQueueTransition,
                withdrawalQueueHash,
                verifierConfig,
                signatures
            )) revert InvalidQuorumCertificate();

        // These are strictly not necessary, but we'll assert them here since they are cheap while
        // the prover doesn't (yet) enforce them.
        //   - continuity:  prevDepositNumber must equal where we last left off
        //   - monotonic:   the queue can only advance (nextDepositNumber >= prevDepositNumber)
        //   - in-range:    cannot process more deposits than have been enqueued
        if (
            depositQueueTransition.prevDepositNumber != lastProcessedDepositNumber
                || depositQueueTransition.nextDepositNumber
                    < depositQueueTransition.prevDepositNumber
                || depositQueueTransition.nextDepositNumber > depositCount
        ) {
            revert InvalidDepositTransition();
        }

        // Verify proof (handles both direct and ancestry modes)
        bool valid = IVerifier(verifier)
            .verify(
                zoneId,
                tempoBlockNumber,
                anchorBlockNumber,
                anchorBlockHash,
                withdrawalBatchIndex + 1,
                blockTransition,
                depositQueueTransition,
                withdrawalQueueHash,
                verifierConfig,
                proof
            );
        if (!valid) revert InvalidProof();

        // Update state
        withdrawalBatchIndex++;
        blockHash = blockTransition.nextBlockHash;
        lastSyncedTempoBlockNumber = tempoBlockNumber;
        lastProcessedDepositNumber = depositQueueTransition.nextDepositNumber;
        zoneHeight = nextZoneHeight;

        uint256 assignedQueueIndex = _withdrawalQueue.enqueue(withdrawalQueueHash);

        // Emit event after state updates
        emit BatchSubmitted(
            withdrawalBatchIndex,
            assignedQueueIndex,
            depositQueueTransition.nextProcessedHash,
            blockHash,
            withdrawalQueueHash,
            lastProcessedDepositNumber
        );
    }

    function _verifySettlement(
        uint256 nextZoneHeight,
        uint64 tempoBlockNumber,
        uint64 anchorBlockNumber,
        bytes32 anchorBlockHash,
        BlockTransition calldata blockTransition,
        DepositQueueTransition calldata depositQueueTransition,
        bytes32 withdrawalQueueHash,
        bytes calldata verifierConfig,
        bytes[] memory signatures
    )
        internal
        view
        returns (bool)
    {
        uint256 threshold = sequencerThreshold;
        if (
            nextZoneHeight <= zoneHeight || signatures.length < threshold
                || signatures.length > _sequencers.length
        ) {
            return false;
        }

        bytes32 structHash = keccak256(
            abi.encode(
                SETTLEMENT_ATTESTATION_TYPEHASH,
                zoneId,
                sequencerSetVersion,
                nextZoneHeight,
                withdrawalBatchIndex + 1,
                verifier,
                tempoBlockNumber,
                anchorBlockNumber,
                anchorBlockHash,
                keccak256(abi.encode(blockTransition)),
                keccak256(abi.encode(depositQueueTransition)),
                withdrawalQueueHash,
                keccak256(verifierConfig)
            )
        );
        bytes32 digest = keccak256(abi.encodePacked("\x19\x01", _domainSeparator(), structHash));
        address[] memory recovered = new address[](signatures.length);

        for (uint256 i = 0; i < signatures.length; ++i) {
            bytes memory signature = signatures[i];
            address signer;
            // The shared TIP-1020 verifier owns signature-format and canonicality checks.
            // Convert its reverts into `false` so the public verifier remains non-reverting.
            try StdPrecompiles.SIGNATURE_VERIFIER.recover(digest, signature) returns (
                address recoveredSigner
            ) {
                signer = recoveredSigner;
            } catch {
                return false;
            }
            if (signer == address(0) || !isSequencer(signer)) return false;
            for (uint256 j = 0; j < i; ++j) {
                if (recovered[j] == signer) return false;
            }
            recovered[i] = signer;
        }

        return signatures.length >= threshold;
    }

    function _domainSeparator() internal view returns (bytes32) {
        return keccak256(
            abi.encode(
                EIP712_DOMAIN_TYPEHASH, NAME_HASH, VERSION_HASH, block.chainid, address(this)
            )
        );
    }

}
